%let rpt_mth = 31AUG2026;
%let rpt_dt  = %sysfunc(inputn(&rpt_mth, date9.));
%let rpt_dtm = "&rpt_mth:00:00:00"dt;
%let nxt_dtm = "%sysfunc(putn(%eval(&rpt_dt + 1), date9.)):00:00:00"dt;

%let mode    = DERIVE;
%let tgt     = INDIVIDUAL_ALLOWANCE;
%let num_tol = 0.0005;

%if %upcase(&mode) = CHECK %then %do;
    %let act_keep = &tgt;
    %let act_ren  = rename=(&tgt = ACT_&tgt);
    %let act_out  = ACT_&tgt MATCH;
%end;
%else %do;
    %let act_keep = ;
    %let act_ren  = ;
    %let act_out  = ;
%end;

data WORK.mstr_derived(keep=PROC_DTE V_ACCOUNT_NUMBER V_IFRS_STAGE_CODE
                            N_EAD_AMOUNT_RCY DCF_AMT INDIVIDUAL_ALLOWANCE &act_out);
    retain PROC_DTE V_ACCOUNT_NUMBER V_IFRS_STAGE_CODE
           N_EAD_AMOUNT_RCY DCF_AMT INDIVIDUAL_ALLOWANCE &act_out;
    format INDIVIDUAL_ALLOWANCE 24.3;
    set LBFRS9.T_MTH_FRS9_RDL_MSTR_LIST(keep=PROC_DTE V_ACCOUNT_NUMBER
                                             V_D_ACCOUNT_STATUS
                                             V_IFRS_STAGE_CODE
                                             N_EAD_AMOUNT_RCY DCF_AMT &act_keep &act_ren);
    where PROC_DTE >= &rpt_dtm and PROC_DTE < &nxt_dtm
          and V_D_ACCOUNT_STATUS = "Active";

    call missing(INDIVIDUAL_ALLOWANCE);

    if strip(V_IFRS_STAGE_CODE) = 'STAGE3' and not missing(DCF_AMT) then
        INDIVIDUAL_ALLOWANCE = max(0, N_EAD_AMOUNT_RCY - DCF_AMT);

    %if %upcase(&mode) = CHECK %then %do;
        length MATCH $1;
        if      missing(&tgt) and missing(ACT_&tgt) then MATCH = 'Y';
        else if missing(&tgt) or  missing(ACT_&tgt) then MATCH = 'N';
        else if abs(&tgt - ACT_&tgt) <= &num_tol    then MATCH = 'Y';
        else MATCH = 'N';
    %end;
run;
